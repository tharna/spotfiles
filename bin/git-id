#!/usr/bin/perl
# vim: sts=4 sw=4 ts=4 expandtab:
use strict;
use warnings;
use Cwd;

my $git_dir = `git rev-parse --git-dir`;
chomp $git_dir;

my $gitids = "$git_dir/gitids.txt";

my $color = 'always';
my $STATUS_DEFAULT = 0;
my $STATUS_SHORT = 1;
my $status_style = $STATUS_DEFAULT;

while (scalar @ARGV && $ARGV[0] =~ /^-/) {
    my $option = shift @ARGV;
    if ($option =~ /--color=(always|auto|never)/) {
        $color = $1;
    } elsif ($option eq '--short') {
        $status_style = $STATUS_SHORT;
    } else {
        print "Unknown option: $option\n";
        exit 1;
    }
}

my @status_option_for = ( '', '--short' );
my $status_format = $status_option_for[$status_style];
my $git_cmd = "git -c color.status=$color status $status_format @ARGV";
open my $git_status, "$git_cmd|"
    or die "Error: $!";

open my $cache, ">$gitids"
    or die "Error: $!";

# Headers
print $cache "cwd: " . getcwd . "\n";
print $cache "status-format: $status_format\n";
print $cache "\n";

my $c = 1;
while (my $line = <$git_status>) {
    if ($status_style == $STATUS_DEFAULT) {
        if ($line =~ /#\t/) {
            $line =~ s/#\t/#$c\t/;
            $c += 1;
        }
    } elsif ($status_style == $STATUS_SHORT) {
        if ($line !~ /^#/) {
            $line =~ s/^/$c /;
            $c += 1;
        }
    }
    print $line;
    $line =~ s/\e\[\d*m//g;
    print $cache $line;
}
close $git_status;
close $cache;
